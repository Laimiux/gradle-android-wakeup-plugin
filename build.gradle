group 'com.antyzero.wakeup'
version '0.0.5-SNAPSHOT'

apply plugin: 'groovy'
apply plugin: 'maven-publish'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    compile 'com.android.tools.build:gradle:2.0.0'
    compile 'com.android.tools.ddms:ddmlib:24.3.0'

    testCompile 'junit:junit:4.12'
}

jar {
    manifest {
        attributes 'Implementation-Title': 'WakeUp plugin wakes tested Android devices up',
                'Implementation-Version': version,
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Built-Gradle': gradle.gradleVersion
    }
}

task integrationTest(type: GradleBuild, dependsOn: ['clean', 'publishToMavenLocal']) {
    dir = new File('../example')
    tasks = ['clean', 'connectedAndroidTest']
}

publishToMavenLocal.dependsOn 'clean'

publishing {

    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId project.group
            artifactId 'wakeup-plugin'
            version project.version
        }
    }
}